buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'nu.validator:validator:16.3.3'
    }
}

plugins {
    id 'com.github.eerohele.saxon-gradle' version '0.3.0'
}

defaultTasks 'xslt'

xslt {
    stylesheet 'xsl/html5.xsl'

    // Transform every .xml file in the xml directory.
    input fileTree(dir: 'xml', include: '*.xml')

    // Set XSLT parameters.
    parameters(
        title: 'Purchase Order',
        padding: '0.625rem'
    )

    // Set Saxon configuration file
    config 'config/configuration.xml'

    catalog 'catalog.xml'

    // Validate results with HTML5 validator.
    finalizedBy 'validate'
}

import nu.validator.client.SimpleCommandLineValidator

task validate(type: JavaExec) {
    classpath = buildscript.configurations.classpath
    main = SimpleCommandLineValidator.getName()
    args xslt.getOutputFiles()
}
